plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystoreProperties = new Properties()
if (System.getenv('CI') != null) {
    keystoreProperties.setProperty('build.storePassword', System.getenv('STORE_PASSWORD'))
    keystoreProperties.setProperty('build.keyAlias', System.getenv('KEY_ALIAS'))
    keystoreProperties.setProperty('build.keyPassword', System.getenv('KEY_PASSWORD'))
}

def keystorePropertiesFile = rootProject.file('keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        release {
            storeFile file('../release.keystore')
            storePassword keystoreProperties['build.storePassword']
            keyAlias keystoreProperties['build.keyAlias']
            keyPassword keystoreProperties['build.keyPassword']
        }
    }
    compileSdk 35

    namespace "it.kapfer.digitalesamt.liberator"
    defaultConfig {
        applicationId android.namespace
        minSdk 21
        targetSdk 35
        versionCode 5
        versionName "2.2"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    compileOnly 'de.robv.android.xposed:api:82'
}